plugins {
    id 'de.undercouch.download' version '3.3.0'
}

apply plugin: 'com.android.library'
apply plugin: 'de.undercouch.download'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.sevar83'

import de.undercouch.gradle.tasks.download.Download
import org.gradle.internal.os.OperatingSystem;

gradle.projectsEvaluated {
    preBuild.dependsOn(buildSqlite)
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName project.version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    // ndk gradle support is still very bad so it's built with the regular ndk-build
    sourceSets.main.jni.srcDirs = ['src/main/none']
}

dependencies {
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

ext {
    sqliteDistributionUrl = 'http://sqlite.org/2016/sqlite-amalgamation-3150100.zip'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task downloadSqlite(type: Download) {
    src project.sqliteDistributionUrl
    dest 'src/main/jni/ndk-modules/sqlite/sqlite.zip'
}

task installSqlite(dependsOn: downloadSqlite, type: Copy) {
    from zipTree(downloadSqlite.dest).matching {
        include '*/sqlite3.*'
        eachFile { it.setPath(it.getName()) }
    }
    into 'src/main/jni/ndk-modules/sqlite'
}

def ndkDir = System.getenv("NDK_HOME")
if (ndkDir == null) {
    def propertiesFile = project.rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        Properties properties = new Properties()
        properties.load(propertiesFile.newDataInputStream())
        ndkDir = properties.getProperty('ndk.dir')
        if (ndkDir == null) {
            throw GradleScriptException("Either NDK_HOME or ndk.dir in local.properties should point to Android NDK!")
        }
    } else {
        throw GradleScriptException("Either NDK_HOME or ndk.dir in local.properties should point to Android NDK!")
    }
}

task buildSqlite(dependsOn: installSqlite, type: Exec) {
    println("NDK directory: $ndkDir")

    // Add NDK_DEBUG=1 for debug symbols or V=1 for verbose building output
    if (OperatingSystem.current().linux) {
        commandLine "$ndkDir/ndk-build", '-j8', '-C', file('src/main/jni').absolutePath
    } else if (OperatingSystem.current().windows) {
        commandLine "$ndkDir/ndk-build.cmd", '-j8', '-C', file('src/main/jni').absolutePath
    } else {
        throw GradleScriptException("Unsupported OS")
    }
}

task ndkClean(type: Exec, description: 'Clean NDK Binaries') {
    if (OperatingSystem.current().linux) {
        commandLine "$ndkDir/ndk-build", 'clean', '-C', file('src/main/jni').absolutePath
    } else if (OperatingSystem.current().windows) {
        commandLine "$ndkDir/ndk-build.cmd", 'clean', '-C', file('src/main/jni').absolutePath
    } else {
        throw GradleScriptException("Unsupported OS")
    }
}

clean.dependsOn 'ndkClean'